    .pos 0
    irmovq stack, %rsp      # 设置栈指针 (非常重要!)
    call main               # 调用主函数
    halt                    # 停机

# Sample linked list
.align 8
ele1:
    .quad 0x00a
    .quad ele2
ele2:
    .quad 0x0b0
    .quad ele3
ele3:
    .quad 0xc00
    .quad 0

main:
    irmovq ele1, %rdi       # 传递参数: 链表头指针放在 %rdi
    call rsum_list           # 调用你的函数
    ret

# sum_list - Sum the elements of a linked list
# start in %rdi
rsum_list:
    xorq %rax, %rax
    andq %rdi, %rdi
    je end
    mrmovq (%rdi), %rdx
    mrmovq 8(%rdi), %rdi
    pushq %rdx
    call rsum_list
    popq %rdx
    addq %rdx, %rax
end:
    ret

# Stack starts here and grows to lower addresses
    .pos 0x200
stack:
